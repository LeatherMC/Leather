plugins {
    id 'java'
    id 'java-gradle-plugin'
}

def props = new Properties()
file('src/gradle.properties').withInputStream {
    props.load(it)
}

group = props.getProperty("group")
archivesBaseName = props.getProperty("name")
version = props.getProperty("version")

def lombok = 'org.projectlombok:lombok:1.18.12'

repositories {
    mavenCentral()

    maven {
        url 'https://repo.mcstats.org/content/repositories/releases/'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    implementation 'org.apache.logging.log4j:log4j-core:2.13.3'
    implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'

    implementation 'net.minecraft:launchwrapper:1.3'

    compileOnly lombok
    annotationProcessor lombok
    testCompileOnly lombok
    testAnnotationProcessor lombok
}

// Generate Sources
task genSources {
    doFirst {
        // Gradle Properties
        def f = file('build/generated/net/leathermc/leather/GProps.java')
        f.parentFile.mkdirs()
        f.write """package net.leathermc.leather;

import lombok.Getter;

public class GProps {
    @Getter
    private static final String version = "$version";
    @Getter
    private static final String name = "$archivesBaseName";
    @Getter
    private static final String group = "$group";
}
"""
    }
}
compileJava.dependsOn genSources
sourceSets.main.java {
    srcDir 'build/generated'
}
